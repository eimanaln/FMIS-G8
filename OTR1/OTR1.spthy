theory OTR1
begin

builtins: signing, diffie-hellman

rule Register_key:
  [ Fr(~k) ] --[ Register($A, ~k) ]-> [ !Ltk($A, ~k), !Pk($A, pk(~k)), Out(pk(~k)) ]

rule Reveal_key:
  [ !Ltk(A, k) ] --[ Reveal(A) ]-> [ Out(k) ]


rule Init_A:
  [ Fr(~idA), !Ltk(A, ka), !Pk(B, pkb) ]

  --[ Init('I', A, ~idA, B), Init_('I') ]->

  [ St_A_1(A, B, ~idA, ka, pkb) ]


rule Init_B:
  [ Fr(~idB), !Pk(A, pka), !Ltk(B, kb) ]

  --[ Init('R', B, ~idB, A), Init_('R')]->

  [ St_B_1(A, B, ~idB, pka, kb) ]


rule A_1_send:
  let gx = 'g'^~x
      signgx = sign(gx, ka)
  in
  [ St_A_1(A, B, idA, ka, pkb), Fr(~x) ]

  --[ Send(A, signgx),  ]->

  [ St_A_2(A, B, idA, ka, pkb, ~x), Out(<gx, signgx, pk(ka)>) ]


rule B_1_recv:
  [ St_B_1(A, B, idB, pka, kb), In(<gx, signgx, pka>) ]

  --[ Recv(B, signgx), _restrict(verify(signgx, gx, pka) = true) ]->

  [ St_B_2(A, B, idB, pka, kb, gx) ]


rule B_2_send:
  let gy = 'g'^~y
      signgy = sign(gy, kb)
  in
  [ St_B_2(A, B, idB, pka, kb, gx), Fr(~y) ]

  --[ Send(B, signgy), Running(B, A, <'I','R', gx, gy>) ]->

  [ St_B_3(A, B, idB, pka, kb, gx, ~y), Out(<gy, signgy, pk(kb)>) ]


rule A_2_recv:
  [ St_A_2(A, B, idA, ka, pkb, x), In(<gy, signgy, pkb>) ]

  --[ Recv(B, signgy), _restrict(verify(signgy, gy, pkb) = true),
      Commit(A, B, <'I','R', 'g'^x, gy>), Finish() ]->

  [ St_A_3(A, B, idA, ka, pkb, x, gy) ]


lemma executable: exists-trace
  " Ex #i.
      Finish() @i &
      not (Ex A #j. Reveal(A)@j)"

restriction one_init_per_role_at_most:
  " All R #i #j. Init_(R)@i & Init_(R)@j ==> (#i=#j) "

restriction no_selftalk:
  " All R A id B #i. Init(R, A, id, B)@i ==> not (A=B) "

end
